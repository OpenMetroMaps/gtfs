wrapper {
    gradleVersion = '7.6.2'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        mavenCentral()
    }
}

configure([project(':gtfs4j-core'),
        project(':gtfs4j-csv-core'),
        project(':gtfs4j-csv-reader'),
        project(':gtfs4j-csv-writer'),
        project(':gtfs4j-csv-util')]) {

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    def global = new File('info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        pomInfo.name = pomInfoL.name
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "$pomInfo.name"
                    description = "$pomInfo.description"
                    url = "$pomInfo.url"
                    licenses {
                        license {
                            name = "$pomInfo.licenseName"
                            url = "$pomInfo.licenseUrl"
                            distribution = "$pomInfo.licenseDistribution"
                        }
                    }
                }
            }
        }
    }
}
